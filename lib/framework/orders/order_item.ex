defmodule Framework.Orders.OrderItem do
  @moduledoc false
  use Ash.Resource,
    otp_app: :framework,
    domain: Framework.Orders,
    data_layer: AshPostgres.DataLayer,
    authorizers: [Ash.Policy.Authorizer],
    extensions: [AshJsonApi.Resource, AshGraphql.Resource]

  alias Framework.Orders.Changes.AssignBatchCodeAndCost

  @plan_load [
    :quantity,
    :planned_qty_sum,
    :completed_qty_sum,
    item: [:name, :sku],
    order: [:reference, :delivery_date, customer: [:full_name]]
  ]

  postgres do
    table "orders_items"
    repo Framework.Repo
  end

  json_api do
    type "order-item"

    routes do
      base("/order-items")
      get(:read)
      index :read
      post(:create)
      patch(:update)
    end
  end

  graphql do
    type :order_item

    queries do
      get(:get_order_item, :read)
      list(:list_order_items, :read)
    end

    mutations do
      create :create_order_item, :create
      update :update_order_item, :update
    end
  end

  actions do
    defaults [:read, :destroy]

    create :create do
      primary? true
      accept [:item_id, :quantity, :unit_price, :status]

      change {AssignBatchCodeAndCost, []}
    end

    update :update do
      primary? true
      require_atomic? false

      accept [
        :quantity,
        :status,
        :consumed_at,
        :material_cost,
        :labor_cost,
        :overhead_cost,
        :unit_cost
      ]
    end

    read :in_range do
      description "Order items whose order delivery_date falls within a datetime range."

      argument :start_date, :utc_datetime do
        allow_nil? false
      end

      argument :end_date, :utc_datetime do
        allow_nil? false
      end

      argument :item_ids, {:array, :uuid} do
        allow_nil? true
        default nil
      end

      argument :exclude_order_id, :uuid do
        allow_nil? true
        default nil
      end

      prepare build(load: [:item, :order])

      # filter by the parent order's delivery_date
      filter expr(
               order.delivery_date >= ^arg(:start_date) and order.delivery_date <= ^arg(:end_date)
             )

      # optionally filter by items
      filter expr(is_nil(^arg(:item_ids)) or item_id in ^arg(:item_ids))

      # optionally exclude items from a given order (useful during updates)
      filter expr(is_nil(^arg(:exclude_order_id)) or order_id != ^arg(:exclude_order_id))
    end

    read :plan_pending do
      get? false

      argument :to, :utc_datetime do
        allow_nil? false
      end

      prepare build(load: @plan_load)
      filter expr(order.delivery_date <= ^arg(:to))
    end
  end

  policies do
    # API key scope check
    policy always() do
      authorize_if {Framework.Accounts.Checks.ApiScopeCheck, []}
    end

    policy action_type([:create, :read, :update, :destroy]) do
      forbid_unless Framework.Accounts.Checks.ActorBelongsToTenant
    end

    # Public read allowed for `:in_range` (capacity checks)
    bypass action(:in_range) do
      authorize_if always()
    end

    # Other reads/writes restricted to staff/admin
    policy action_type(:read) do
      authorize_if expr(^actor(:role) in [:staff, :admin])
    end

    policy action_type([:create, :update, :destroy]) do
      authorize_if expr(^actor(:role) in [:staff, :admin])
    end
  end

  preparations do
    prepare Framework.Preparations.SetTenant
  end

  changes do
    change Framework.Changes.SetTenant
  end

  multitenancy do
    strategy :context
  end

  attributes do
    uuid_primary_key :id

    attribute :unit_price, :decimal do
      allow_nil? false
    end

    attribute :quantity, :decimal do
      allow_nil? false
    end

    attribute :status, Framework.Orders.OrderItem.Types.Status do
      allow_nil? false
      default :todo
    end

    attribute :consumed_at, :utc_datetime do
      allow_nil? true
      description "Timestamp indicating materials were consumed for this item"
    end

    attribute :batch_code, :string do
      allow_nil? true
      description "Production batch identifier generated when marking the item done"
    end

    attribute :material_cost, :decimal do
      allow_nil? false
      default 0
      description "Material cost allocated to this order item during batch completion"
    end

    attribute :labor_cost, :decimal do
      allow_nil? false
      default 0
      description "Labor cost allocated to this order item during batch completion"
    end

    attribute :overhead_cost, :decimal do
      allow_nil? false
      default 0
      description "Overhead cost allocated to this order item during batch completion"
    end

    attribute :unit_cost, :decimal do
      allow_nil? false
      default 0
      description "Per-unit production cost captured at batch completion"
    end

    timestamps()
  end

  relationships do
    belongs_to :order, Framework.Orders.Order do
      allow_nil? false
    end

    belongs_to :item, Framework.Project.Item do
      allow_nil? false
    end

    belongs_to :bom, Framework.Project.BOM do
      allow_nil? true
    end

    belongs_to :production_batch, Framework.Orders.ProductionBatch do
      allow_nil? true
    end

    has_many :order_item_lots, Framework.Orders.OrderItemLot

    has_many :allocations, Framework.Orders.OrderItemBatchAllocation
  end

  calculations do
    calculate :cost, :decimal, expr(quantity * unit_price)
  end

  aggregates do
    sum :planned_qty_sum, :allocations, :planned_qty
    sum :completed_qty_sum, :allocations, :completed_qty
  end
end
